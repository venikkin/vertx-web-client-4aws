plugins {
    id 'java'
    id 'maven'
    id 'signing'
}

group = 'com.venikkin'
archivesBaseName = 'vertx-web-client-4aws'
version = '0.1'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    // main
    // vertx
    implementation platform('io.vertx:vertx-dependencies:4.0.3')
    implementation 'io.vertx:vertx-web-client'
    // aws
    implementation platform('software.amazon.awssdk:bom:2.15.69')
    implementation 'software.amazon.awssdk:auth'
    implementation 'software.amazon.awssdk:regions'

    // test
    // vertx
    testImplementation 'io.vertx:vertx-junit5'

    // aws
    testImplementation 'software.amazon.awssdk:cloudformation'

    // junit + asserts
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation("org.assertj:assertj-core:3.19.0")
    testImplementation("net.javacrumbs.json-unit:json-unit-assertj:2.24.0")
}

test {
    useJUnitPlatform()
}

task deployTestStack(type: Exec) {
    workingDir 'test-stack'
    commandLine './deploy-stack.sh'
}

task removeTestStack(type: Exec) {
    workingDir 'test-stack'
    commandLine './remove-stack.sh'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'vertx-web-client-4aws'
                packaging 'jar'
                artifactId 'vertx-web-client-4aws'
                description 'Vertx web client supporting Sigv4'
                url 'https://github.com/venikkin/vertx-web-client-4aws'
                inceptionYear '2021'

                scm {
                    connection 'scm:https://github.com/venikkin/vertx-web-client-4aws.git'
                    developerConnection 'scm:git://github.com/venikkin/vertx-web-client-4aws.git'
                    url 'https://github.com/venikkin/vertx-web-client-4aws'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'venikkin'
                        name 'Evgenii Rubanov'
                        email 'venikkin@gmail.com'
                    }
                }
            }
        }
    }
}

