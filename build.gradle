plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.venikkin'
archivesBaseName = 'vertx-web-client-4aws'
version = '0.2'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    // main
    // vertx
    implementation platform('io.vertx:vertx-dependencies:4.3.7')
    implementation 'io.vertx:vertx-web-client'
    // aws
    implementation platform('software.amazon.awssdk:bom:2.19.15')
    implementation 'software.amazon.awssdk:auth'
    implementation 'software.amazon.awssdk:regions'

    // test
    // vertx
    testImplementation 'io.vertx:vertx-junit5'

    // aws
    testImplementation 'software.amazon.awssdk:cloudformation'

    // json
    testImplementation("com.fasterxml.jackson.core:jackson-core:2.14.1")
    testImplementation("com.fasterxml.jackson.core:jackson-databind:2.14.1")

    // junit + asserts
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation("org.assertj:assertj-core:3.24.1")
    testImplementation("net.javacrumbs.json-unit:json-unit-assertj:2.36.0")
}

test {
    useJUnitPlatform()
}

task deployTestStack(type: Exec) {
    workingDir 'test-stack'
    commandLine './deploy-stack.sh'
}

task removeTestStack(type: Exec) {
    workingDir 'test-stack'
    commandLine './remove-stack.sh'
}

publishing {
    publications{
        mavenJava(MavenPublication){
            groupId = 'com.venikkin'
            artifactId = 'vertx-web-client-4aws'
            version = '0.2'

            from components.java

            pom {
                name = 'vertx-web-client-4aws'
                packaging = 'jar'
                artifactId = 'vertx-web-client-4aws'
                description = 'Vertx web client supporting Sigv4'
                url = 'https://github.com/venikkin/vertx-web-client-4aws'
                inceptionYear = '2023'

                scm {
                    connection = 'scm:https://github.com/venikkin/vertx-web-client-4aws.git'
                    developerConnection = 'scm:git://github.com/venikkin/vertx-web-client-4aws.git'
                    url = 'https://github.com/venikkin/vertx-web-client-4aws'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'venikkin'
                        name = 'Evgenii Rubanov'
                        email = 'venikkin@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
