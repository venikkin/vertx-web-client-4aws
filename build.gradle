plugins {
    id 'java'
    id 'com.bmuschko.nexus' version '2.3.1'
}

group 'com.venikkin'
version '0.1'

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

modifyPom {
    project {
        name 'vertx-web-client-4aws'
        description 'Vertx web client supporting Sigv4'
        url 'https://github.com/venikkin/vertx-web-client-4aws'
        inceptionYear '2021'

        scm {
            url 'https://github.com/venikkin/vertx-web-client-4aws'
            connection 'scm:https://github.com/venikkin/vertx-web-client-4aws.git'
            developerConnection 'scm:git://github.com/venikkin/vertx-web-client-4aws.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'venikkin'
                name 'Evgenii Rubanov'
                email 'venikkin@gmail.com'
            }
        }

    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = false
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

dependencies {
    // main
    // vertx
    implementation platform('io.vertx:vertx-dependencies:4.0.3')
    implementation 'io.vertx:vertx-web-client'
    // aws
    implementation platform('software.amazon.awssdk:bom:2.15.69')
    implementation 'software.amazon.awssdk:auth'
    implementation 'software.amazon.awssdk:regions'

    // test
    // vertx
    testImplementation 'io.vertx:vertx-junit5'

    // aws
    testImplementation 'software.amazon.awssdk:cloudformation'

    // junit + asserts
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation("org.assertj:assertj-core:3.19.0")
    testImplementation("net.javacrumbs.json-unit:json-unit-assertj:2.24.0")
}

test {
    useJUnitPlatform()
}

task deployTestStack(type: Exec) {
    workingDir 'test-stack'
    commandLine './deploy-stack.sh'
}

task removeTestStack(type: Exec) {
    workingDir 'test-stack'
    commandLine './remove-stack.sh'
}

